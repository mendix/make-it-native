apply plugin: "com.android.application"
apply plugin: "com.facebook.react"
apply plugin: "com.google.gms.google-services"
apply plugin: "com.google.firebase.crashlytics"
apply plugin: "kotlin-android"
apply plugin: "kotlin-kapt"
apply plugin: 'org.jetbrains.kotlin.plugin.compose'
apply plugin: "androidx.navigation.safeargs.kotlin"

react {
    /* Variants */
    //   The list of variants to that are debuggable. For those we're going to
    //   skip the bundling of the JS bundle and the assets. By default is just 'debug'.
    //   If you add flavors like lite, prod, etc. you'll have to list your debuggableVariants.
     debuggableVariants = ["productionDebug", "productionRelease"]

    /* Autolinking */
    autolinkLibrariesWithApp()
}

/**
 * Set this to true to Run Proguard on Release builds to minify the Java bytecode.
 */
def enableProguardInReleaseBuilds = false

android {
    ndkVersion rootProject.ndkVersion
    buildToolsVersion rootProject.ext.buildToolsVersion
    compileSdkVersion rootProject.compileSdkVersion

    namespace "com.mendix.developerapp"
    defaultConfig {
        applicationId "com.mendix.developerapp.mx10"
        minSdkVersion rootProject.minSdkVersion
        targetSdkVersion rootProject.targetSdkVersion
        versionCode 513
        versionName "5.0.2"
        missingDimensionStrategy "react-native-camera", "general"
        testBuildType System.getProperty("testBuildType", "debug")
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        buildConfigField "boolean", "DETOX", "false"
        buildConfigField "String", "RUNTIME_URL", '""'
        buildConfigField "boolean", "OTA", "false"
    }
    signingConfigs {
        debug {
            Properties debugProps = new Properties()
            def propFile = new File('./keystores/debug.keystore.properties')
            if (propFile.canRead()) {
                debugProps.load(new FileInputStream(propFile))

                storeFile file(debugProps["key.store"])
                storePassword debugProps["key.store.password"]
                keyAlias debugProps["key.alias"]
                keyPassword debugProps["key.alias.password"]
            } else {
                print 'debug.keystore.properties not found'
            }
        }
        release {
           Properties releaseProps = new Properties()
           def propFile = new File('./keystores/debug.keystore.properties')
           if (propFile.canRead()) {
               releaseProps.load(new FileInputStream(propFile))

               storeFile file(releaseProps["key.store"])
               storePassword releaseProps["key.store.password"]
               keyAlias releaseProps["key.alias"]
               keyPassword releaseProps["key.alias.password"]
           } else {
               print 'local.properties not found'
           }
        }
    }
    buildTypes {
        debug {
            signingConfig signingConfigs.debug
        }
        release {
            signingConfig signingConfigs.release
            minifyEnabled enableProguardInReleaseBuilds
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    flavorDimensions "version"
    productFlavors {
        production {
            dimension "version"
        }
    }
    buildFeatures {
        compose true
        viewBinding true
        dataBinding true
        buildConfig true
    }
    composeOptions {
        kotlinCompilerExtensionVersion  compose_version
    }
    packagingOptions {
        pickFirst "**/*.so"
    }
}

dependencies {
    // React Native Core
    implementation "com.facebook.react:react-android:0.77.0"
    implementation("com.facebook.react:hermes-android")

    // Mendix dependencies
    implementation project(":mendixnative-release")

    // Dev app dependencies
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation "android.arch.lifecycle:extensions:1.1.1"
    implementation "com.google.android.material:material:1.12.0"
    implementation "androidx.core:core-ktx:$androidx_core_version"
    implementation "androidx.activity:activity-ktx:1.9.3"
    implementation "androidx.fragment:fragment-ktx:1.8.4"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$androidx_lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$androidx_lifecycle_version"
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
    implementation "androidx.appcompat:appcompat:$appcompat_version"
    implementation "androidx.navigation:navigation-common-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-runtime-ktx:$nav_version"
    implementation "androidx.preference:preference-ktx:1.2.1"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"
    implementation "com.android.support.constraint:constraint-layout:$constraint_layout_version"
    implementation("com.journeyapps:zxing-android-embedded:4.1.0") { transitive = false }
    implementation "com.google.zxing:core:3.4.0"
    implementation "me.leolin:ShortcutBadger:1.1.22@aar"
    implementation platform("com.google.firebase:firebase-bom:32.3.1")
    implementation "com.google.firebase:firebase-crashlytics"
    implementation "com.google.firebase:firebase-analytics"
    implementation "com.google.firebase:firebase-messaging"
    implementation "com.google.code.gson:gson:2.10.1"
    implementation "com.github.yuriy-budiyev:code-scanner:2.1.0"
    implementation "com.fasterxml.jackson.core:jackson-core:2.11.3"
    implementation "com.fasterxml.jackson.core:jackson-annotations:2.11.3"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.11.3"
    implementation "androidx.activity:activity-compose:1.9.3"
    implementation "androidx.compose.ui:ui:$compose_ui_version"
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_ui_version"
    implementation "androidx.compose.foundation:foundation:$compose_ui_version"
    implementation 'androidx.compose.material:material:1.2.0'
    debugImplementation "androidx.compose.ui:ui-tooling:$compose_ui_version"
    debugImplementation "androidx.compose.ui:ui-test-manifest:$compose_ui_version"

    implementation 'androidx.security:security-crypto:1.1.0-alpha03'

    implementation "androidx.camera:camera-camera2:$camerax_version"
    implementation "androidx.camera:camera-lifecycle:$camerax_version"
    implementation "androidx.camera:camera-view:$camerax_version"
    implementation 'com.google.mlkit:barcode-scanning:17.3.0'
    implementation "com.google.guava:guava:31.0.1-android"
    implementation("androidx.core:core-splashscreen:1.0.1")
    implementation project(':react-native-localize')

    androidTestImplementation "junit:junit:4.13.2"
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_ui_version"
}

configurations.all {
    resolutionStrategy {
        force "androidx.lifecycle:lifecycle-viewmodel:$androidx_lifecycle_version"
    }
}

apply from: file("../../node_modules/@mendix/native/androidlib/mendix.gradle"); applyMendixGradle(project)
apply from: "../../node_modules/react-native-vector-icons/fonts.gradle"
